module FileSystem

  use int.Int
  use import map.Map as M
  use import set.Fset as F
  use seq.Seq, seq.Mem, seq.Distinct

  lemma append_empty: forall s: seq 'a.
    s ++ empty == s

  lemma empty_length: forall s: seq 'a.
    length s = 0 <-> s == empty

  (* Arbitrary type for a node of our tree *)
  type elt

  (* Verifies if two nodes are equal *)
  val equal (e1 e2 : elt) : bool
    ensures { result <-> e1 = e2 }

  (* Indicates if two nodes make an edge in our tree *)
  predicate edge (x y : elt) (f : elt -> elt) =
    x <> y /\ f x = y

  predicate path (f: elt -> elt) (x y: elt) (p: seq elt) =
    let n = length p in
    n = 0 /\ x = y
  \/
    n > 0 /\
    p[0] = x /\
    edge p[n - 1] y f /\
    distinct p /\
    (forall i. 0 <= i < n - 1 -> edge p[i] p[i + 1] f) /\
    (forall i. 0 <= i < n     -> p[i] <> y)

  predicate reachable (f: elt -> elt) (x y: elt) =
    exists p. path f x y p

  predicate disjoint_seq (s1 s2: seq 'a) =
    forall i j. 0 <= i < length s1 -> 0 <= j < length s2 -> s1[i] <> s2[j]

  (* This lemma states that if there is a path from [from] to [middle]
     and if there is a path from [middle] to [until] then there is a path
     from [from] to [until] *)
  lemma trans_path: forall from middle until f pth1 pth2.
    path f from middle pth1 -> path f middle until pth2 ->
    disjoint_seq pth1 pth2 -> from <> until ->
    (forall j. 0 <= j < length pth1 -> pth1[j] <> until) ->
    path f from until (pth1 ++ pth2)

  lemma path_edge: forall x y : elt, f : elt -> elt.
    edge x y f -> path f x y (cons x empty)

  lemma path_edge_snoc: forall n x y : elt, f : elt -> elt, pth : seq elt.
    n <> y -> not (mem y pth) -> (* FIXME: try to relax such property *)
    distinct (snoc pth x) -> path f n x pth -> edge x y f ->
    path f n y (snoc pth x)

  (* TO BE PROVED *)
  let lemma unique_path_pairwise (x y: elt) (f: elt -> elt) (pth1 pth2: seq elt)
    requires { path f x y pth1 }
    requires { path f x y pth2 }
    requires { length pth1 = length pth2 }
    ensures  { forall i. 0 <= i < length pth1 -> pth1[i] = pth2[i] }
  = if length pth1 = 0 then ()
    else begin
      let ref s1 = singleton pth1[0] in
      let ref s2 = singleton pth2[0] in
      for i = 1 to length pth1 - 1 do
        invariant { length s1 = length s2 }
        invariant { s1 == pth1 [.. i] }
        invariant { s2 == pth2 [.. i] }
        invariant { s1 == s2 }
        assert { s1[i - 1] = s2[i - 1] };
        s1 <- snoc s1 pth1[i];
        s2 <- snoc s2 pth2[i]
      done;
      assert { s1 == pth1 };
      assert { s2 == pth2 }
    end

  lemma unique_path_equal: forall x y: elt, f: elt -> elt, pth1 pth2: seq elt.
    path f x y pth1 -> path f x y pth2 ->
    length pth1 = length pth2 &&
    forall i. 0 <= i < length pth1 -> pth1[i] = pth2[i]

  lemma unique_path_ext: forall x y: elt, f: elt -> elt, pth1 pth2: seq elt.
    path f x y pth1 -> path f x y pth2 ->
    pth1 == pth2

  lemma unique_path: forall x y: elt, f: elt -> elt, pth1 pth2: seq elt.
    path f x y pth1 -> path f x y pth2 ->
    pth1 = pth2

  constant n: elt (* constant only used for the state witness *)

  type state = {
    mutable tree : elt -> elt;    (* mapping of the tree son -> parent *)
    mutable root : elt;           (* root of the tree *)
    mutable dom  : fset elt;      (* domain of the tree mapping function *)
  } invariant { F.mem root dom }
    invariant { tree root = root }
    invariant { forall x. F.mem x dom -> F.mem (tree x) dom }
    invariant { forall x. F.mem x dom -> reachable tree x root }
    invariant { forall x. F.mem x dom -> reachable tree root x -> x = root }
    (* invariant { forall x y. F.mem x dom -> F.mem y dom ->  *)
    by { tree = (fun _ -> n); root = n; dom = F.singleton n }

  lemma new_element: forall n w p: elt, s: state, l: seq elt.
    path s.tree w s.root l -> not (mem n l) ->
    F.mem w s.dom -> F.mem p s.dom -> not (F.mem n s.dom) ->
    w <> n -> n <> p ->
    path (M.set s.tree n p) w s.root l

  lemma parent_move: forall x c np: elt, f: elt -> elt, p: seq elt.
    x <> np -> c <> np -> x <> c -> not (reachable f np c) ->
    path f x c p -> distinct (cons c p) -> not (mem np p) ->
    (path (M.set f c np) x c p)

  let lemma move_lemma_aux (x c np : elt) (s : state) (p p1 : seq elt)
    requires { c <> np }
    requires { x <> c }
    requires { not (reachable s.tree np c) }
    requires { path s.tree x s.root p }
    requires { (not reachable s.tree x c) }
    requires { distinct p }
    requires { distinct p1 }
    requires { (path (M.set s.tree c np) x s.root p1) }
    requires { length p1 = length p }
    ensures  { forall i. 0 <= i < length p1 -> p1[i] = p[i] }
  = if length p1 = 0 then ()
    else begin
      let ref s1 = singleton p1[0] in
      let ref s2 = singleton p[0] in
      for i = 1 to length p1 - 1 do
        invariant { length s1 = length s2 }
        invariant { s1 == p1 [.. i] }
        invariant { s2 == p [.. i] }
        invariant { s1 == s2 }
        assert { s1[i - 1] = s2[i - 1] };
        s1 <- snoc s1 p1[i];
        s2 <- snoc s2 p[i]
      done;
      assert { s1 == p1 };
      assert { s2 == p };
      assert { p1 == p }
    end 
  
  lemma move_lemma: forall x c np: elt, s: state, p p1: seq elt.
    c <> np -> x <> c -> not (reachable s.tree np c) ->
    path s.tree x s.root p -> (not reachable s.tree x c) ->
    distinct p -> distinct p1 -> (path (M.set s.tree c np) x s.root p1) ->
    p == p1
    
  let lemma non_intermediate (x c np : elt) (f : elt -> elt) (p: seq elt)
      requires { not (reachable f np c) }
      requires { path f x c p }
      requires { forall p1 y. path f x y p1 -> not (mem np p1) }
      ensures  { not (mem np p) }
   =  if length p = 0 then ()
      else begin
        let ref s1 = singleton p[0] in
        for i = 1 to length p - 1 do
          invariant { s1 == p [.. i] }
          invariant { not (mem np s1) }
          assert { s1[i - 1] <> np  };
          s1 <- snoc s1 p[i]
        done;
        
      end 

  lemma non_intermediate_reachability: forall f x c np p.
    not (reachable f np c) -> path f x c p -> 
    (forall p1 y. path f x y p1 -> not (mem np p1)) ->
    not (mem np p)
    
  let lemma up_reach_aux (initial middle final : elt) (f : elt -> elt) (p1 p2 : seq elt)
      requires { path f middle final p2 }
      requires { path f initial middle p1 }
      requires { final <> initial }
      requires { middle <> initial }
      requires { middle <> final }
      requires { (forall x. reachable f middle x -> not (mem x p1)) }
      ensures  { disjoint_seq p1 p2 }
  = if length p1 = length p2 then
      if length p1 = 0 then ()
      else begin
       let ref s1 = singleton p1[0] in
       let ref s2 = singleton p2[0] in
       for i = 1 to length p1 - 1 do
        invariant { length s1 = length s2 }
        invariant { disjoint_seq s1 s2 }
        assert { s1[i - 1] <>  s2[i - 1] };
        s1 <- snoc s1 p1[i];
        s2 <- snoc s2 p2[i]
       done;
      end
    else ()

  lemma up_reach: forall final initial middle : elt,
    f : elt -> elt, p1 p2 : seq elt.
    path f middle final p2 -> path f initial middle p1 ->
    final <> initial -> middle <> initial -> middle <> final ->
    (forall x. reachable f middle x -> not (mem x p1)) ->
    disjoint_seq p1 p2

  (* lemma up_reach: forall final initial middle x y : elt,
    f : elt -> elt, p1 p2 : seq elt.
    path f middle final p2 -> path f initial middle p1 ->
    mem x p1 -> mem y p2 -> x <> y *)

  (* lemma not_in_path: forall n: elt, state: state. *)
  (*   not (F.mem p s.dom) -> *)
  (*   forall x w: elt, p: seq elt. *)
  (*     F.mem x s.dom -> F.mem x s.dom -> path s.tree x w p -> *)
  (*     not (n *)

  let ghost add (n p : elt) (s : state) : unit
    requires { not F.mem n s.dom }
    requires { F.mem p s.dom }
    requires { p <> s.root /\ n <> s.root }
    requires { forall x. F.mem x s.dom -> not (reachable s.tree x n)}
    requires { forall x. F.mem x s.dom -> reachable s.tree x s.root ->
               exists p. path s.tree x s.root p /\ not (mem n p) }
    ensures  { s.tree = M.set (old s.tree) n p }
    ensures  { edge n p s.tree }
    ensures  { s.dom = F.add n (old s).dom }
  = s.tree <- M.set s.tree n p;
    s.dom  <- F.add n s.dom

  let ghost remove (n p : elt) (s : state) : unit
    requires { F.mem n s.dom }
    requires { edge n p s.tree }
    requires { forall x. s.tree x <> n }
    requires { n <> s.root }
    writes   { s.dom }
    ensures  { s.dom = F.remove n (old s).dom }
  = s.dom <- F.remove n s.dom

  let ghost move (p c np : elt) (s : state) : unit
    requires { F.mem p s.dom }
    requires { F.mem np s.dom }
    requires { F.mem c s.dom }
    requires { edge c p s.tree }
    requires { not (reachable s.tree np c) }
    requires { not (reachable s.tree p c) }
    requires { forall x. reachable s.tree x c -> not (reachable s.tree np x) }
    requires { c <> s.root }
    requires { p <> np }
    requires { c <> np /\ c <> p }
    writes   { s.tree }
    ensures  { edge c np s.tree }
    ensures  { s.tree = M.set (old s.tree) c np }
  = s.tree <- M.set s.tree c np;

end